apply plugin: "com.android.application"

apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: "kotlin-android-extensions"

apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: "org.jetbrains.dokka"

android {
    compileSdkVersion 29
    dataBinding {
        enabled = true
        version = "3.4.1"
    }

    defaultConfig {
        applicationId "com.quickdev.projectdashboard"
        minSdkVersion 26
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField 'String', 'BASE_URL', '\"https://project-dashboard.azurewebsites.net/api/\"'
        buildConfigField 'Boolean', 'IsEmulator', 'false'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.incremental": "true"]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            debuggable true
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "target"
    productFlavors {
        emulator {
            dimension "target"

            buildConfigField 'String', 'BASE_URL', '\"https://10.0.2.2:5001/api/\"'
            buildConfigField 'Boolean', 'IsEmulator', 'true'
        }

        device {
            dimension "target"
        }
    }


    kotlinOptions {
        jvmTarget = "1.8"
    }

    kapt {
        generateStubs = true
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = 'docs'
    }

    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDirs += sharedTestDir
        }
        androidTest {
            java.srcDirs += sharedTestDir
        }
    }
}

dependencies {
    implementation fileTree(include: ["*.jar"], dir: "libs")
    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$version_kotlin"

    implementation "androidx.core:core:$version_core"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation "androidx.constraintlayout:constraintlayout:$version_constraint_layout"

    // Material Design
    implementation "com.google.android.material:material:$version_material"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // Navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$version_navigation"
    implementation "androidx.navigation:navigation-ui-ktx:$version_navigation"

    // Room and Lifecycle dependencies
    implementation "androidx.room:room-runtime:$version_room"
    kapt "androidx.room:room-compiler:$version_room"
    kapt "com.android.databinding:compiler:$version_databinding"
    implementation "androidx.lifecycle:lifecycle-extensions:$version_lifecycle_extensions"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$version_lifecycle_rc"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$version_lifecycle_rc"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$version_lifecycle_rc"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$version_coroutine"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$version_coroutine"

    // Moshi for converting JSON
    implementation "com.squareup.moshi:moshi:$version_moshi"
    implementation "com.squareup.moshi:moshi-kotlin:$version_moshi"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:$version_retrofit"
    implementation "com.squareup.retrofit2:converter-moshi:${version_retrofit}"
    implementation 'com.squareup.okhttp3:logging-interceptor:4.2.2'
    implementation "com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:$version_retrofit_coroutines_adapter"

    // Additional Libs
    implementation 'com.auth0.android:jwtdecode:1.4.0'
    implementation 'de.hdodenhof:circleimageview:3.0.1'
    implementation 'com.airbnb.android:lottie:3.3.0'

    // AIDAN FOLLESTAD (https://af.codes/)
    implementation "com.afollestad.material-dialogs:core:$version_matDialogs"
    implementation "com.afollestad.material-dialogs:input:$version_matDialogs"
    implementation "com.afollestad.material-dialogs:bottomsheets:$version_matDialogs"
    implementation "com.afollestad.material-dialogs:lifecycle:$version_matDialogs"
    implementation 'com.afollestad:vvalidator:0.5.2'
    implementation 'com.afollestad:recyclical:1.1.1'
    implementation 'com.afollestad:material-cab:2.0.1'

    // Testing
    testImplementation 'junit:junit:4.12'
    testImplementation 'io.mockk:mockk:1.9.3.kotlin12'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$version_coroutine"
    testImplementation 'android.arch.core:core-testing:1.1.1'

    // UI Testing with Espresso
    androidTestImplementation "androidx.test.espresso:espresso-core:$version_espresso"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$version_espresso"
    androidTestImplementation "androidx.test.ext:junit:$version_junit"
    androidTestImplementation "androidx.test:runner:$version_testrunner"
    androidTestImplementation "androidx.test:rules:$version_testrules"
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"
}
